// see https://spec.json5.org/#syntactic-grammar and
// https://spec.json5.org/#lexical-grammar

text = _{
  soi ~ value ~ eoi
}

value = _{
  null |
  boolean |
  string |
  number |
  object |
  array
}

null = @{
  "null"
}

boolean = @{
  "true" |
  "false"
}

string = @{
  "\"" ~ double_string_character* ~ "\"" |
  "'" ~ single_string_character* ~ "'"
}

number = @{
  numeric_literal |
  "+" ~ numeric_literal |
  "-" ~ numeric_literal
}

object = {
  "{" ~ "}" |
  "{" ~ key_value_pairs ~ ","? ~ "}"
}

array = {
  "[" ~ "]" |
  "[" ~ element_list ~ ","? ~ "]"
}

key_value_pairs = _{
  (key_value_pair ~ ",")* ~ key_value_pair
}

key_value_pair = {
  key ~ ":" ~ value
}

key = _{
  identifier |
  string
}

identifier = @{
  identifier_start ~ (identifier_part)*
}

element_list = _{
  (value ~ ",")* ~ value
}

numeric_literal = _{
  hex_integer_literal |
  decimal_literal |
  "Infinity" |
  "NaN"
}

decimal_literal = _{
  decimal_integer_literal ~ "." ~ decimal_digits? ~ exponent_part? |
  "." ~ decimal_digits ~ exponent_part? |
  decimal_integer_literal ~ exponent_part?
}

decimal_integer_literal = _{
  "0" |
  non_zero_digit ~ decimal_digits?
}

decimal_digits = _{
  decimal_digit+
}

decimal_digit = _{
  '0'..'9'
}

non_zero_digit = _{
  '1'..'9'
}

exponent_part = _{
  exponent_indicator ~ signed_integer
}

exponent_indicator = _{
  ^"e"
}

signed_integer = _{
  decimal_digits |
  "+" ~ decimal_digits |
  "-" ~ decimal_digits
}

hex_integer_literal = _{
  "0x" ~ hex_digit+ |
  "0X" ~ hex_digit+
}

hex_digit = _{
  '0'..'9' |
  'a'..'f' |
  'A'..'F'
}

double_string_character = _{
  "\u{2028}" |
  "\u{2029}" |
  "\\" ~ escape_sequence |
  line_continuation |
  !("\"" | "\\" | line_terminator) ~ any
}

single_string_character = _{
  "\u{2028}" |
  "\u{2029}" |
  "\\" ~ escape_sequence |
  line_continuation |
  !("'" | "\\" | line_terminator) ~ any
}

line_continuation = _{
  "\\" ~ line_terminator_sequence
}

line_terminator_sequence = _{
  "\u{000D}" ~ "\u{000A}" |
  line_terminator
}

line_terminator = _{
  "\u{000A}" |
  "\u{000D}" |
  "\u{2028}" |
  "\u{2029}"
}

// TODO
identifier_start = _{
  'a'..'z' |
  'A'..'Z' |
  "$" |
  "_" |
  "\\" ~ unicode_escape_sequence
}

// TODO
identifier_part = _{
  identifier_start |
  '0'..'9'
}

escape_sequence = _{
  character_escape_sequence |
  "0" |
  hex_escape_sequence |
  unicode_escape_sequence
}

character_escape_sequence = _{
  single_escape_character |
  non_escape_character
}

single_escape_character = _{
  "'" |
  "\"" |
  "\\" |
  "b" |
  "f" |
  "n" |
  "r" |
  "t" |
  "v"
}

non_escape_character = _{
  !(escape_character | line_terminator) ~ any
}

escape_character = _{
  single_escape_character |
  decimal_digit |
  "x" |
  "u"
}

hex_escape_sequence = _{
  "x" ~ hex_digit{2}
}

unicode_escape_sequence = _{
  "u" ~ hex_digit{4}
}

whitespace = _{
  line_terminator_sequence |
  "\u{0009}" |
  "\u{000B}" |
  "\u{000C}" |
  "\u{0020}" |
  "\u{00A0}" |
  "\u{1680}" |
  "\u{2000}" |
  "\u{2001}" |
  "\u{2002}" |
  "\u{2003}" |
  "\u{2004}" |
  "\u{2005}" |
  "\u{2006}" |
  "\u{2007}" |
  "\u{2008}" |
  "\u{2009}" |
  "\u{200A}" |
  "\u{202F}" |
  "\u{205F}" |
  "\u{3000}" |
  "\u{FEFF}"
}

comment = _{
  "/*" ~ (!"*/" ~ any)* ~ "*/" |
  "//" ~ (!line_terminator ~ any)*
}
